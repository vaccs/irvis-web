
/**
 ******************  Script Configuration ******************
 */
buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()
    mavenCentral()

    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }

  }

  dependencies {
    classpath 'com.sandec.jpro:jpro-plugin-gradle:2020.1.6'
    classpath 'org.openjfx:javafx-plugin:0.0.9'
    classpath 'org.antlr:antlr4:4.7.2'
  }

}


/**
 ******************  Java Configuration ******************
 */
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'antlr'

compileJava {
  sourceCompatibility = 11
  targetCompatibility = 11
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    implementation 'com.jfoenix:jfoenix:9.0.10'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-listener", "-package", "org.vaccs.eevis.parser" ]
    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}


javafx {
    version='15.0.1'
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
}


/**
 ******************  jpro Configuration ******************
 */
apply plugin: 'com.sandec.jpro'


/**
 * App Main Class
 */
mainClassName = 'com.jpro.eevis.UIExpressionEvaluation'

/**
 * jpro settings
 */
jpro {
  // for debugging
  // JVMArgs << '-agentlib:jdwp=transport=dt_socket,server=n,address=5006,suspend=y'

  JVMArgs << '-Xmx1000m'

  //jpro server port
  port = 8080

}
